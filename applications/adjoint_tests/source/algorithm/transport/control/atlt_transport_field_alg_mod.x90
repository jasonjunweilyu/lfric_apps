!-----------------------------------------------------------------------------
! (c) Crown copyright 2025 Met Office. All rights reserved.
! The file LICENCE, distributed with this code, contains details of the terms
! under which the code may be used.
!-----------------------------------------------------------------------------
!> @brief Module containing adjoint test for atl_transport_field_alg
module atlt_transport_field_alg_mod

  use r_tran_field_mod,                    only : r_tran_field_type
  use field_mod,                           only : field_type
  use function_space_mod,                  only : function_space_type
  use constants_mod,                       only : r_def, i_def, l_def
  use log_mod,                             only : log_event,         &
                                                  log_scratch_space, &
                                                  LOG_LEVEL_INFO,    &
                                                  LOG_LEVEL_DEBUG,   &
                                                  LOG_LEVEL_ERROR
  use mesh_mod,                            only : mesh_type
  use model_clock_mod,                     only : model_clock_type
  use finite_element_config_mod,           only : element_order_h, &
                                                  element_order_v
  use fs_continuity_mod,                   only : W2, W3
  use function_space_collection_mod,       only : function_space_collection
  use transport_metadata_collection_mod,   only : transport_metadata_collection
  use transport_metadata_mod,              only : transport_metadata_type
  use tl_transport_runtime_collection_mod, &
                                           only : tl_transport_runtime
  use transport_runtime_alg_mod,           only : transport_runtime_type
  use transport_runtime_collection_mod,    only : get_transport_runtime
  use inner_from_runtime_rdef_alg_mod,     only : dry_flux_prod_rdef,     &
                                                  dry_flux_inp_prod_rdef, &
                                                  flux_prod_rdef,         &
                                                  flux_inp_prod_rdef,     &
                                                  rho_d_prod_rdef,        &
                                                  rho_d_inp_prod_rdef,    &
                                                  field_n_prod_rdef,      &
                                                  field_n_inp_prod_rdef,  &
                                                  adv_wind_prod_rdef,     &
                                                  adv_wind_inp_prod_rdef
  use init_from_runtime_alg_mod,           only : init_atl_end_con_step_fields, &
                                                  init_adv_wind_field
  use transport_enumerated_types_mod,      only : direction_3d, &
                                                  direction_h,  &
                                                  direction_v

  implicit none

  private
  public :: atlt_transport_field_alg

  contains

  !=============================================================================
  !> @brief   Adjoint test for atl_transport_field_alg.
  !> @details Passes if adjoint is transpose of tangent linear.
  !>          Determined by testing the equality of inner products <Mx, Mx> and <AMx, x>,
  !>          where M is the tangent linear and A is the adjoint.
  !> @param[in]  mesh         The model mesh
  !> @param[in]  model_clock  The model clock
  subroutine atlt_transport_field_alg( mesh, model_clock )

    use tl_transport_field_mod,              only : tl_transport_field
    use atl_transport_field_alg_mod,         only : atl_transport_field_alg

    implicit none

    ! Arguments
    type(mesh_type), pointer, intent(in) :: mesh
    type(model_clock_type),   intent(in) :: model_clock

    ! Arguments for tl and adj calls
    type(field_type)                       :: field_np1
    type(field_type)                       :: field_n
    type(field_type)                       :: ls_field_n
    real(kind=r_def)                       :: model_dt
    type(transport_metadata_type), pointer :: transport_metadata

    ! Copies of input fields used in inner products
    type(field_type)                            :: field_np1_input
    type(field_type)                            :: field_n_input

    ! Variables used to handle calculations for fields stored in tl_transport_runtime
    type(r_tran_field_type)                            :: tr_field_n_inp
    type(r_tran_field_type), dimension(:), allocatable :: tr_dry_flux_inp
    type(r_tran_field_type), dimension(:), allocatable :: tr_flux_inp
    type(r_tran_field_type), dimension(:), allocatable :: tr_rho_d_inp
    type(r_tran_field_type)                            :: tr_wind_h_inp
    type(r_tran_field_type)                            :: tr_wind_v_inp
    type(r_tran_field_type)                            :: tr_wind_inp
    integer(kind=i_def)                                :: num_steps
    integer(kind=i_def)                                :: mesh_id

    ! Variables for initialising fields
    type(function_space_type), pointer :: vector_space_w2_ptr
    type(function_space_type), pointer :: vector_space_w3_ptr

    ! Inner products
    real(kind=r_def) :: inner1
    real(kind=r_def) :: field_np1_inner_prod
    real(kind=r_def) :: field_n_inner_prod
    real(kind=r_def) :: inner2
    real(kind=r_def) :: fnp1_fnp1_input_inner_prod
    real(kind=r_def) :: fn_fn_input_inner_prod

    ! Test parameters and variables
    real(kind=r_def), parameter :: overall_tolerance = 1500.0_r_def
    real(kind=r_def) :: machine_tol
    real(kind=r_def) :: relative_diff

    type(transport_runtime_type), pointer :: transport_runtime

    mesh_id = mesh % get_id()
    model_dt = real( model_clock%get_seconds_per_step(), r_def )
    transport_metadata => transport_metadata_collection%get_transport_metadata('density')
    transport_runtime => get_transport_runtime( mesh )

    vector_space_w2_ptr => function_space_collection % get_fs( mesh, element_order_h, element_order_v, W2 )
    vector_space_w3_ptr => function_space_collection % get_fs( mesh, element_order_h, element_order_v, W3 )
    call field_np1 % initialise( vector_space = vector_space_w3_ptr )
    call field_n % initialise( vector_space = vector_space_w3_ptr )
    call ls_field_n % initialise( vector_space = vector_space_w3_ptr )

    call field_np1_input % initialise( vector_space = vector_space_w3_ptr )
    call field_n_input % initialise( vector_space = vector_space_w3_ptr )

    field_np1_inner_prod = 0.0_r_def
    field_n_inner_prod = 0.0_r_def

    ! Initialise values and call the tangent-linear alg.
    call invoke( setval_random( field_np1 ),             &
                 setval_x( field_np1_input, field_np1 ), &
                 setval_random( field_n ),               &
                 setval_x( field_n_input, field_n ),     &
                 setval_random( ls_field_n ) )

    ! Setting up inputs for tl_transport_runtime.
    num_steps = tl_transport_runtime % get_num_dry_steps()
    allocate( tr_dry_flux_inp( num_steps ) )
    allocate( tr_flux_inp( num_steps - 1 ) )
    allocate( tr_rho_d_inp( num_steps + 1 ) )
    call init_atl_end_con_step_fields( mesh, num_steps, tr_field_n_inp, tr_dry_flux_inp, tr_flux_inp, tr_rho_d_inp )
    call init_adv_wind_field( mesh, tr_wind_h_inp, direction_h )
    call init_adv_wind_field( mesh, tr_wind_v_inp, direction_v )
    call init_adv_wind_field( mesh, tr_wind_inp, direction_3d )

    ! Tangent linear
    call tl_transport_field( field_np1, field_n, ls_field_n, &
                             model_dt, transport_metadata )
    call tl_transport_runtime % dec_tracer_step_ctr()
    call transport_runtime % dec_tracer_step_ctr()

    ! < Mx, Mx >
    call invoke( x_innerproduct_x( field_np1_inner_prod, field_np1 ), &
                 x_innerproduct_x( field_n_inner_prod, field_n ) )

    inner1 = 0.0_r_def
    inner1 = inner1 + field_np1_inner_prod
    inner1 = inner1 + field_n_inner_prod
    write(log_scratch_space, *) "atlt_transport_field inner products:"
    call log_event( log_scratch_space, LOG_LEVEL_DEBUG )
    write(log_scratch_space, *) "field_np1 inner product = ", field_np1_inner_prod
    call log_event( log_scratch_space, LOG_LEVEL_DEBUG )
    write(log_scratch_space, *) "field_n inner product = ", field_n_inner_prod
    call log_event( log_scratch_space, LOG_LEVEL_DEBUG )
    call dry_flux_prod_rdef( inner1, mesh_id )
    call flux_prod_rdef( inner1, mesh_id )
    call rho_d_prod_rdef( inner1, mesh )
    call field_n_prod_rdef( inner1 )
    call adv_wind_prod_rdef( inner1, mesh_id, direction_h )
    call adv_wind_prod_rdef( inner1, mesh_id, direction_v )
    call adv_wind_prod_rdef( inner1, mesh_id, direction_3d )

    ! Adjoint alg call and inner products
    fnp1_fnp1_input_inner_prod = 0.0_r_def
    fn_fn_input_inner_prod = 0.0_r_def

    ! Adjoint
    call atl_transport_field_alg( field_np1, field_n, ls_field_n, &
                                  model_dt, transport_metadata )
    call tl_transport_runtime % dec_tracer_step_ctr()
    call transport_runtime % dec_tracer_step_ctr()

    ! < AMx, x >
    call invoke( x_innerproduct_y( fnp1_fnp1_input_inner_prod, &
                                   field_np1,                  &
                                   field_np1_input ),          &
                 x_innerproduct_y( fn_fn_input_inner_prod,     &
                                   field_n,                    &
                                   field_n_input ) )

    inner2 = 0.0_r_def
    inner2 = inner2 + fnp1_fnp1_input_inner_prod
    inner2 = inner2 + fn_fn_input_inner_prod
    call dry_flux_inp_prod_rdef( inner2, mesh_id, tr_dry_flux_inp )
    call flux_inp_prod_rdef( inner2, mesh_id, tr_flux_inp )
    call rho_d_inp_prod_rdef( inner2, mesh, tr_rho_d_inp )
    call field_n_inp_prod_rdef( inner2, tr_field_n_inp )
    call adv_wind_inp_prod_rdef( inner2, mesh_id, tr_wind_h_inp, direction_h )
    call adv_wind_inp_prod_rdef( inner2, mesh_id, tr_wind_v_inp, direction_v )
    call adv_wind_inp_prod_rdef( inner2, mesh_id, tr_wind_inp, direction_3d )

    ! Test the inner-product values for equality, allowing for the precision of the active variables
    machine_tol = spacing( max( abs( inner1 ), abs( inner2 ) ) )
    relative_diff = abs( inner1 - inner2 ) / machine_tol
    if ( relative_diff < overall_tolerance ) then
      write( log_scratch_space, * ) "PASSED tl_transport_field:", inner1, inner2, relative_diff
      call log_event( log_scratch_space, LOG_LEVEL_INFO )
    else
      write( log_scratch_space, * ) "FAILED tl_transport_field:", inner1, inner2, relative_diff
      call log_event( log_scratch_space, LOG_LEVEL_ERROR )
    end if

    deallocate( tr_dry_flux_inp, &
                tr_flux_inp,     &
                tr_rho_d_inp )

  end subroutine atlt_transport_field_alg

end module atlt_transport_field_alg_mod
